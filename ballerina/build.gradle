/*
 * Copyright (c) 2024, WSO2 LLC. (http://www.wso2.com)
 *
 * WSO2 LLC. licenses this file to you under the Apache License,
 * Version 2.0 (the "License"); you may not use this file except
 * in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied. See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

import org.apache.tools.ant.taskdefs.condition.Os

plugins {
    id 'io.ballerina.plugin'
}

description = 'Ballerina - IBM CICS Transaction Gateway Connector'

def packageName = "ibm.ctg"
def packageOrg = "ballerinax"
def tomlVersion = stripBallerinaExtensionVersion("${project.version}")
def ballerinaTomlFilePlaceHolder = new File("${project.rootDir}/build-config/resources/Ballerina.toml")
def ballerinaTomlFile = new File("$project.projectDir/Ballerina.toml")

def stripBallerinaExtensionVersion(String extVersion) {
    if (extVersion.matches(project.ext.timestampedVersionRegex)) {
        def splitVersion = extVersion.split('-')
        if (splitVersion.length > 3) {
            def strippedValues = splitVersion[0..-4]
            return strippedValues.join('-')
        } else {
            return extVersion
        }
    } else {
        return extVersion.replace("${project.ext.snapshotVersion}", "")
    }
}

ballerina {
    packageOrganization = packageOrg
    module = packageName
    testCoverageParam = "--code-coverage --coverage-format=xml"
    isConnector = true
    platform = "java17"
}

configurations {
    externalJars
}

dependencies {
    // Jackson dependencies
    externalJars(group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: "${jacksonVersion}") {
        transitive = false
    }
    externalJars(group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: "${jacksonVersion}") {
        transitive = false
    }
    externalJars(group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: "${jacksonVersion}") {
        transitive = false
    }

    // Apache commons-io dependencies
    externalJars(group: 'commons-io', name: 'commons-io', version: "${commonsIoVersion}") {
        transitive = false
    }

    // Apache http components dependencies
    externalJars(group: 'org.apache.httpcomponents.core5', name: 'httpcore5', version: "${apacheHttpComponentsVersion}") {
        transitive = false
    }
    externalJars(group: 'org.apache.httpcomponents.core5', name: 'httpcore5-h2', version: "${apacheHttpComponentsVersion}") {
        transitive = false
    }
    externalJars(group: 'org.apache.httpcomponents.client5', name: 'httpclient5', version: "${apacheHttpClientVersion}") {
        transitive = false
    }

    // Gerenimo j2ee spec dependency
    externalJars(group: 'org.apache.geronimo.specs', name: 'geronimo-j2ee-connector_1.6_spec', version: "${geronimoJ2eeSpecVersion}") {
        transitive = false
    }
}

task updateTomlFiles {
    doLast {
        def newBallerinaToml = ballerinaTomlFilePlaceHolder.text.replace("@project.version@", project.version)
        newBallerinaToml = newBallerinaToml.replace("@toml.version@", tomlVersion)
        newBallerinaToml = newBallerinaToml.replace("@jackson.version@", jacksonVersion)
        newBallerinaToml = newBallerinaToml.replace("@commons.io.version@", commonsIoVersion)
        newBallerinaToml = newBallerinaToml.replace("@http.core.version@", apacheHttpComponentsVersion)
        newBallerinaToml = newBallerinaToml.replace("@http.client.version@", apacheHttpClientVersion)
        newBallerinaToml = newBallerinaToml.replace("@geronimo.j2ee.version@", geronimoJ2eeSpecVersion)
        ballerinaTomlFile.text = newBallerinaToml
    }
}

task commitTomlFiles {
    doLast {
        project.exec {
            ignoreExitValue true
            if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                commandLine 'cmd', '/c', "git commit -m \"[Automated] Update the toml files\" Ballerina.toml Dependencies.toml"
            } else {
                commandLine 'sh', '-c', "git commit -m '[Automated] Update the toml files' Ballerina.toml Dependencies.toml"
            }
        }
    }
}

build.dependsOn copyToLib
build.dependsOn ":${packageName}-native:build"

test.dependsOn ":${packageName}-native:build"

publish.dependsOn build
publishToMavenLocal.dependsOn build
